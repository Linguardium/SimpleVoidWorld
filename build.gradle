plugins {
    id 'java'
    id 'fabric-loom' version '0.4-SNAPSHOT'
    id 'com.matthewprenger.cursegradle' version "1.4.0"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
    maven {
        name = "Modmuss50"
        url = "https://maven.fabricmc.net/"
    }
	mavenLocal()
}

def ENV = System.getenv()
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

version = getVersionName()
logger.lifecycle("Version = " + version)

dependencies {
    minecraft "com.mojang:minecraft:1.16.1"
    mappings "net.fabricmc:yarn:1.16.1+build.5:v2"
    modImplementation "net.fabricmc:fabric-loader:0.8.8+build.202"

    //Fabric api
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.13.1+build.370-1.16"
}

group = 'SimpleVoidWorld'

curseforge {
    if (ENV.CURSE_API_KEY) {
        apiKey = ENV.CURSE_API_KEY
    }
    project {
        id = '254466'
        changelog = 'A changelog can be found at https://github.com/modmuss50/SimpleVoidWorld'
        releaseType = 'release'
        addGameVersion '1.16.1'
        addGameVersion 'Fabric'
        mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))
        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }
    options {
        forgeGradleIntegration = false
    }
}